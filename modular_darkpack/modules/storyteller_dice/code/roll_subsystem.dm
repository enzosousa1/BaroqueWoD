SUBSYSTEM_DEF(roll)
	name = "Dice Rolling"
	ss_flags = SS_NO_FIRE
	var/on_crit_extra_die_enabled = FALSE
	var/on_crit_extra_success_enabled = FALSE

/datum/controller/subsystem/roll/Initialize()
	on_crit_extra_die_enabled = CONFIG_GET(flag/on_crit_additional_die)
	on_crit_extra_success_enabled = CONFIG_GET(flag/on_crit_additional_success)
	return SS_INIT_SUCCESS

/**
 * Rolls a number of dice according to Storyteller system rules to find
 * success or number of successes.
 *
 * Rolls a number of 10-sided dice, counting them as a "success" if
 * they land on a number equal to or greater than the difficulty. Dice
 * that land on 1 subtract a success from the total, and the minimum
 * difficulty is 2. The number of successes is returned if numerical
 * is true, or the roll outcome (botch, failure, success) as a defined
 * number if false.
 *
 * Arguments:
 * * roller - the mob who is making the role and owns the dice
 * * target - the target we roll against, used selectivly in logic primarly based on what roll_datum you used
 * * roll_datum - path of the datum used for all roll logic. can suppliment many of the args by defining it in this
 * * bonus - amount of dice added ontop of applic stats, can consitute all of the dice if applic stats is left blank.
 * * difficulty - the number that a dice must come up as to count as a success.
 * * applic_stats - A list of types/defines for what stats will be used in the roll.
 * * numerical - whether the proc returns number of successes or outcome (botch, failure, success)
 */
/datum/controller/subsystem/roll/proc/storyteller_roll_datum(mob/living/roller, atom/target, roll_datum = /datum/storyteller_roll, bonus = 0, difficulty, applic_stats, numerical)
	var/datum/storyteller_roll/dice_roll = new roll_datum()
	if(!isnull(difficulty))
		dice_roll.difficulty = difficulty
	if(!isnull(applic_stats))
		dice_roll.applicable_stats = applic_stats
	if(!isnull(numerical))
		dice_roll.numerical = numerical
	return dice_roll.st_roll(roller, target, bonus)

//Config datums for exploding dice
/datum/config_entry/flag/on_crit_additional_success

/datum/config_entry/flag/on_crit_additional_die

// If having 0 dot in abilities can hardlock you out of features
/datum/config_entry/flag/punishing_zero_dots
