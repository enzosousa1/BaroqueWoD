// regenerate_organs override

/datum/species/regenerate_organs(mob/living/carbon/target, datum/species/old_species, replace_current = TRUE, list/excluded_zones, visual_only = FALSE, replace_missing = TRUE)
	. = ..()
	if(target.dna.features[FEATURE_HORNS_NOCTURNE] && can_regenerate_mutant_feature(FEATURE_HORNS_NOCTURNE))
		if(target.dna.features[FEATURE_HORNS_NOCTURNE] != /datum/sprite_accessory/nocturne/horns/none::name && target.dna.features[FEATURE_HORNS_NOCTURNE] != /datum/sprite_accessory/blank::name)
			var/obj/item/organ/replacement = SSwardrobe.provide_type(/obj/item/organ/horns/mutant)
			replacement.Insert(target, special = TRUE, movement_flags = DELETE_IF_REPLACED)
			return .
	var/obj/item/organ/old_part = target.get_organ_slot(ORGAN_SLOT_EXTERNAL_HORNS)
	if(old_part && old_part.type == /obj/item/organ/horns/mutant)
		old_part.Remove(target, special = TRUE, movement_flags = DELETE_IF_REPLACED)
		old_part.moveToNullspace()

// horn toggle

/datum/preference/toggle/nocturne_toggle/horns
	savefile_key = "has_horns_nocturne"
	savefile_identifier = PREFERENCE_CHARACTER
	category = PREFERENCE_CATEGORY_SECONDARY_FEATURES
	feature_key = FEATURE_HORNS_NOCTURNE

// horn options

/datum/preference/choiced/nocturne_feature/horns
	savefile_key = "feature_horns_nocturne"
	savefile_identifier = PREFERENCE_CHARACTER
	main_feature_name = "Horns"
	should_generate_icons = TRUE
	// relevant_organ = /obj/item/organ/horns/mutant
	category = PREFERENCE_CATEGORY_CLOTHING
	feature_key = FEATURE_HORNS_NOCTURNE
	toggle_pref = /datum/preference/toggle/nocturne_toggle/horns

/datum/preference/choiced/nocturne_feature/horns/compile_constant_data()
	var/list/data = ..()
	data[SUPPLEMENTAL_FEATURE_KEY] = /datum/preference/tri_color/horns_color::savefile_key
	return data

/datum/preference/choiced/nocturne_feature/horns/create_default_value()
	return SPRITE_ACCESSORY_NONE

/datum/preference/choiced/nocturne_feature/horns/icon_for(value)
	return generate_horns_icon(get_accessory_for_value(value))

/datum/preference/choiced/nocturne_feature/horns/proc/generate_horns_icon(datum/sprite_accessory/sprite_accessory)
	var/static/datum/universal_icon/body
	if (isnull(body))
		body = uni_icon('modular_nocturne/modules/customization/icons/mob/human/species/anthro/bodyparts.dmi', "anthro_head_m")
	var/datum/universal_icon/final_icon = body.copy()

	if(!isnull(sprite_accessory) && sprite_accessory.icon_state != "none")
		blend_mutant_accessory_preview_layers(final_icon, sprite_accessory.icon, "m_horns_[sprite_accessory.icon_state]_ADJ", shift_direction = NORTH)
		blend_mutant_accessory_preview_layers(final_icon, sprite_accessory.icon, "m_horns_[sprite_accessory.icon_state]_FRONT", shift_direction = NORTH)

	final_icon.crop(9, 18, 23, 32)
	final_icon.scale(32, 32)

	return final_icon

/datum/preference/tri_color/horns_color
	priority = PREFERENCE_PRIORITY_BODY_TYPE
	savefile_key = "horns_nocturne_color"
	savefile_identifier = PREFERENCE_CHARACTER
	category = PREFERENCE_CATEGORY_SUPPLEMENTAL_FEATURES

/datum/preference/tri_color/horns_color/create_default_value()
	return list(sanitize_hexcolor("[pick("7F", "FF")][pick("7F", "FF")][pick("7F", "FF")]"),
	sanitize_hexcolor("[pick("7F", "FF")][pick("7F", "FF")][pick("7F", "FF")]"),
	sanitize_hexcolor("[pick("7F", "FF")][pick("7F", "FF")][pick("7F", "FF")]"))

/datum/preference/tri_color/horns_color/apply_to_human(mob/living/carbon/human/target, value)
	target.dna.features[FEATURE_HORNS_NOCTURNE_COLORS] = value

/datum/preference/tri_color/horns_color/is_valid(value)
	if (!..(value))
		return FALSE
	return TRUE
