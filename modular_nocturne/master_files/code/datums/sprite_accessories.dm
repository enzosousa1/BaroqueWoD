/datum/sprite_accessory
	///Unique key of an accessory. All tails should have "tail", ears "ears" etc.
	var/key = null
	///If an accessory is special, it wont get included in the normal accessory lists
	var/special = FALSE
	///Which color we default to on acquisition of the accessory (such as switching species, default color for character customization etc)
	///You can also put down a a HEX color, to be used instead as the default
	var/default_color
	/// Whether or not this sprite accessory has an additional overlay added to
	/// it as an "inner" part, which is pre-colored.
	var/has_inner = FALSE
	color_src = USE_ONE_COLOR
	///Which layers does this accessory affect
	var/relevant_layers = list(BODY_BEHIND_LAYER, BODY_ADJ_LAYER, BODY_FRONT_LAYER, BODY_FRONT_UNDER_CLOTHES_LAYER, ABOVE_BODY_FRONT_HEAD_LAYER)
	///This is used to determine whether an accessory gets added to someone. This is important for accessories that are "None", which should have this set to false
	var/factual = TRUE
	///Use this as a type path to an organ that this sprite_accessory will be associated. Make sure the organ has 'mutantpart_info' set properly.
	var/obj/item/organ/organ_type
	/// Tells the mutant bodypart overlay responsible for us what layers this sprite accessory actually applies to, and what to call the icon_state
	var/list/color_layer_names = list()

/datum/sprite_accessory/New()
	if(!default_color)
		switch(color_src)
			if(USE_ONE_COLOR)
				default_color = DEFAULT_PRIMARY
			if(USE_MATRIXED_COLORS)
				default_color = DEFAULT_MATRIXED
			else
				default_color = "#FFFFFF"
	color_layer_names["1"] = MUTANT_ACCESSORY_NO_AFFIX // Makes sure single color accessories still work
	if(name == SPRITE_ACCESSORY_NONE)
		factual = FALSE
	if(color_src == USE_MATRIXED_COLORS && default_color != DEFAULT_MATRIXED)
		default_color = DEFAULT_MATRIXED
	if(color_src == USE_MATRIXED_COLORS)
		if(!SSaccessories.cached_mutant_icon_files[icon])
			SSaccessories.cached_mutant_icon_files[icon] = icon_states(new /icon(icon))
		var/list/cached_states = SSaccessories.cached_mutant_icon_files[icon]
		var/found_plain_layer = FALSE
		var/found_primary_layer = FALSE
		for(var/layer in relevant_layers)
			var/layertext = "FRONT"
			switch(layer)
				if(BODY_BEHIND_LAYER)
					layertext = "BEHIND"
				if(BODY_ADJ_LAYER)
					layertext = "ADJ"
				if(BODY_FRONT_UNDER_CLOTHES_LAYER)
					layertext = "FRONT_UNDER"
			var/base_state = "m_[key]_[icon_state]_[layertext]"
			if(base_state in cached_states)
				found_plain_layer = TRUE
			if("[base_state]_primary" in cached_states)
				found_primary_layer = TRUE
			if(!("2" in color_layer_names))
				if("[base_state]_2" in cached_states)
					color_layer_names["2"] = "2"
				else if("[base_state]_secondary" in cached_states)
					color_layer_names["2"] = "secondary"
			if(!("3" in color_layer_names))
				if("[base_state]_3" in cached_states)
					color_layer_names["3"] = "3"
				else if("[base_state]_tertiary" in cached_states)
					color_layer_names["3"] = "tertiary"
		if(found_plain_layer)
			color_layer_names["1"] = MUTANT_ACCESSORY_NO_AFFIX
		else if(found_primary_layer)
			color_layer_names["1"] = "primary"

// sock overrides

/datum/sprite_accessory/clothing/socks
	//All underwear goes in the same file for the sake of digi variants
	icon = 'modular_nocturne/modules/customization/icons/mob/human/accessories/underwear/socks.dmi'
	use_static = TRUE

// undershirt overrides

/datum/sprite_accessory/clothing/undershirt
	icon = 'modular_nocturne/modules/customization/icons/mob/human/accessories/underwear/undershirt.dmi'
	use_static = TRUE
	///Whether this underwear includes a bottom (For Leotards and the likes)
	var/hides_groin = FALSE

// underwear overrides

/datum/sprite_accessory/clothing/underwear
	icon = 'modular_nocturne/modules/customization/icons/mob/human/accessories/underwear/underwear.dmi'
	///Whether this underwear includes a top (Because gender = FEMALE doesn't actually apply here.). Hides breasts, nothing more.
	var/hides_breasts = FALSE

/datum/sprite_accessory/clothing/underwear/male_briefs
	has_digitigrade = TRUE

/datum/sprite_accessory/clothing/underwear/male_boxers
	has_digitigrade = TRUE

/datum/sprite_accessory/clothing/underwear/male_stripe
	has_digitigrade = TRUE

/datum/sprite_accessory/clothing/underwear/male_midway
	has_digitigrade = TRUE

/datum/sprite_accessory/clothing/underwear/male_longjohns
	has_digitigrade = TRUE

/datum/sprite_accessory/clothing/underwear/male_hearts
	has_digitigrade = TRUE

/datum/sprite_accessory/clothing/underwear/male_commie
	has_digitigrade = TRUE

/datum/sprite_accessory/clothing/underwear/male_usastripe
	has_digitigrade = TRUE

/datum/sprite_accessory/clothing/underwear/male_uk
	has_digitigrade = TRUE
